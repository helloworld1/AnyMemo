apply plugin: 'com.android.application'

repositories {
    jcenter()
    google()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        versionCode 232
        versionName "10.11.2"
        minSdkVersion 16
        targetSdkVersion 27
        testApplicationId "org.liberty.android.fantastischmemo.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        multiDexEnabled true
        archivesBaseName = "AnyMemo"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            versionNameSuffix "-${getBuildString()}"
        }
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
        }
    }

    flavorDimensions "mode", "api"

    productFlavors {
        free {
            applicationId "org.liberty.android.fantastischmemo"
            dimension "mode"
        }
        pro {
            applicationId "org.liberty.android.fantastischmemopro"
            dimension "mode"
        }
        dev {
            applicationId "org.liberty.android.fantastischmemodev"
            dimension "mode"
        }
        api21 {
            minSdkVersion 21
            dimension "api"
        }
        api16 {
            minSdkVersion 16
            dimension "api"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

dependencies {
    ext {
        androidSupportLibraryVersion = "27.1.1"
        androidSupportLibraryVersion = "27.1.1"
        daggerVersion = "2.15"
        espressoVersion = "3.0.1"
        ormliteVersion = "5.1"
        supportMultidexVersion = "1.0.3"
        supportConstraintLayoutVersion = "1.1.2"
    }

    implementation "com.android.support:support-core-utils:$ext.androidSupportLibraryVersion"
    implementation "com.android.support:support-fragment:$ext.androidSupportLibraryVersion"
    implementation "com.android.support:cardview-v7:$ext.androidSupportLibraryVersion"
    implementation "com.android.support:design:$ext.androidSupportLibraryVersion"
    implementation "com.android.support:support-annotations:$ext.androidSupportLibraryVersion"
    implementation "com.android.support:customtabs:$ext.androidSupportLibraryVersion"
    implementation "com.android.support:multidex:$ext.supportMultidexVersion"
    implementation "com.android.support.constraint:constraint-layout:$ext.supportConstraintLayoutVersion"

    implementation "com.j256.ormlite:ormlite-core:$ext.ormliteVersion"
    implementation "com.j256.ormlite:ormlite-android:$ext.ormliteVersion"
    implementation "com.squareup.okhttp3:okhttp:3.10.0"

    implementation "io.reactivex.rxjava2:rxjava:2.0.4"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"

    implementation "commons-io:commons-io:2.4"

    implementation "com.google.guava:guava:23.4-android"
    implementation "com.opencsv:opencsv:3.4"
    implementation "com.github.PhilJay:MPAndroidChart:v2.1.6"
    implementation "com.rarepebble:colorpicker:2.0.0"
    implementation "com.simplify:ink:1.0.0"
    implementation "com.google.dagger:dagger:$ext.daggerVersion"
    implementation "com.google.android.gms:play-services-auth:15.0.1"
    compileOnly "javax.annotation:jsr250-api:1.0"

    androidTestImplementation "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:1.4"
    androidTestImplementation "org.mockito:mockito-core:1.10.19"
    androidTestImplementation "com.android.support:support-annotations:$ext.androidSupportLibraryVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$ext.espressoVersion") {
        exclude group: "com.google.code.findbugs"
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-intents:$ext.espressoVersion") {
        exclude group: "com.google.code.findbugs"
    }

    androidTestImplementation "com.android.support.test:runner:1.0.2"

    annotationProcessor "com.google.dagger:dagger-compiler:$ext.daggerVersion"
}

static def getBuildString () {
    def date = new Date()
    def formattedDate = date.format('yyMMdd')
    return "b" + formattedDate
}

task copySecretsIfNotExists << {
    if (file('src/main/java/org/liberty/android/fantastischmemo/AMSecrets.java').exists()) {
        logger.warn('Removing AMSecrets.java since it is now in common package.')
        delete 'src/main/java/org/liberty/android/fantastischmemo/AMSecrets.java'
    }
    if (!file('src/main/java/org/liberty/android/fantastischmemo/common/AMSecrets.java').exists()) {
        logger.warn('Empty AMSecrets.java is created from AMSecrets.java.template. Please fill in the values.')
        copy {
            from "src/main/java/org/liberty/android/fantastischmemo/common/"
            into "src/main/java/org/liberty/android/fantastischmemo/common/"
            rename "AMSecrets.java.template", "AMSecrets.java"
            include "AMSecrets.java.template"
        }
    }
}

preBuild.dependsOn copySecretsIfNotExists 
